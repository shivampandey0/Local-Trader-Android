apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

def versionMajor = 2
def versionMinor = 5
def versionPatch = 4
def versionBuild = 0 // bump for dog food builds, public betas, etc.

def LBC_KEY() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("lbcAccessKey");
}

def LBC_SECRET() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("lbcAccessSecret");
}

def CALLBACK_URL() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("callbackUrl");
}

android {
    kapt {
        generateStubs = true
        correctErrorTypes = true
    }
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        applicationId 'com.thanksmister.bitcoin.localtrader'
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        dev {
            dimension "default"
            applicationId "com.thanksmister.bitcoin.localtrader"
            versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            buildConfigField 'String', 'LBC_KEY', "\"" + LBC_KEY() + "\""
            buildConfigField 'String', 'LBC_SECRET', "\"" + LBC_SECRET() + "\""
            buildConfigField 'String', 'CALLBACK_URL', "\"" + CALLBACK_URL() + "\""
        }
        qa {
            dimension "default"
            applicationId "com.thanksmister.bitcoin.localtrader"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}-QA"
            buildConfigField 'String', 'LBC_KEY', "\"" + LBC_KEY() + "\""
            buildConfigField 'String', 'LBC_SECRET', "\"" + LBC_SECRET() + "\""
            buildConfigField 'String', 'CALLBACK_URL', "\"" + CALLBACK_URL() + "\""
        }
        prod {
            dimension "default"
            applicationId "com.thanksmister.bitcoin.localtrader"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            buildConfigField 'String', 'LBC_KEY', "\"" + LBC_KEY() + "\""
            buildConfigField 'String', 'LBC_SECRET', "\"" + LBC_SECRET() + "\""
            buildConfigField 'String', 'CALLBACK_URL', "\"" + CALLBACK_URL() + "\""
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'com.google.android.gms:play-services-vision:18.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'

    // Room
    implementation "android.arch.persistence.room:runtime:$versions.archRoomVersion"
    kapt "android.arch.persistence.room:compiler:$versions.archRoomVersion"
    implementation "android.arch.persistence.room:rxjava2:$versions.archRoomVersion"
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"

    // OKHttp
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    annotationProcessor "com.google.dagger:dagger-android-processor:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // used for crypto libraries
    implementation 'com.google.guava:guava:24.1-jre'

    // Storage
    // TODO migrate to room or shared preferences
    implementation 'net.grandcentrix.tray:tray:0.12.0'

    // Stetho
    implementation "com.facebook.stetho:stetho:$versions.stetho"
    implementation "com.facebook.stetho:stetho-okhttp3:$versions.stetho"
    implementation "com.facebook.stetho:stetho-urlconnection:$versions.stetho"

    // Rate app
    implementation 'io.github.kobakei:ratethisapp:1.2.0'

    // Crashlytics
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
